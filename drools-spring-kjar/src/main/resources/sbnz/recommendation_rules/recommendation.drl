package recommendation_rules;

import sbnz.skincare.facts.Goal;
import sbnz.skincare.facts.Product;
import sbnz.skincare.facts.SkinTypeCharacteristics;
import sbnz.skincare.facts.Ingredient;
import sbnz.skincare.facts.drools.IngredientScoring;
import sbnz.skincare.facts.drools.ProductScoring;
import sbnz.skincare.facts.drools.RecommendationInput;
import sbnz.skincare.facts.enumerations.SkinCharacteristic;
import sbnz.skincare.facts.enumerations.AgeGroup;
import sbnz.skincare.facts.enumerations.ProductType;
import sbnz.skincare.facts.enumerations.SkinType;
import sbnz.skincare.facts.enumerations.AcneType;
import java.time.LocalDate;
import java.time.Period;
import java.util.List;

declare Serum
end

declare Cleanser
end

declare Moisturizer
end

declare SunScreen
end

declare Exfoliator
end

rule "Age Group - CHILD"
	
	salience 200
	when 
		$r: RecommendationInput(birthday != null, Period.between(birthday, LocalDate.now()).getYears() <= 14 ) 
	then
		System.out.println("Age group - CHILD");
		modify($r) { setAgeGroup(AgeGroup.CHILD) }
end

rule "Age Group - YOUTH"

	salience 200
	when 
		$r: RecommendationInput(birthday != null, Period.between(birthday, LocalDate.now()).getYears() > 14 && Period.between(birthday, LocalDate.now()).getYears() <= 25 ) 
	then
		System.out.println("Age group - YOUTH");
		modify($r) { setAgeGroup(AgeGroup.YOUTH) }
end

rule "Age Group - ADULT"

	salience 200
	when 
		$r: RecommendationInput(birthday != null, Period.between(birthday, LocalDate.now()).getYears() > 25 && Period.between(birthday, LocalDate.now()).getYears() <= 64 ) 
	then
		System.out.println("Age group - ADULT");
		modify($r) { setAgeGroup(AgeGroup.ADULT) }
end

rule "Age Group - SENIOR"

	salience 200
	when 
		$r: RecommendationInput(birthday != null, Period.between(birthday, LocalDate.now()).getYears() > 64 ) 
	then
		System.out.println("Age group - SENIOR");
		modify($r) { setAgeGroup(AgeGroup.SENIOR) }
end

rule "Skin Types" 
	
	salience ((100*$value) / $characteristics.size())
    when
        $s: SkinTypeCharacteristics($type: skinType, $characteristics: skinCharacteristics)
        $r: RecommendationInput(diagnosedSkinType == null, $enteredCharacteristics: skinCharacteristics)
        Number($value: intValue >= 1) from accumulate(
        	SkinCharacteristic($t:this, this memberOf $characteristics) from $enteredCharacteristics,
        	count($t)
        )
    then
    	System.out.println("Skin type: " + $type);
    	System.out.println("Number of skin type charactereistics: " + $value + "/" + $characteristics.size());
    	modify($r) { setDiagnosedSkinType($type) }
end

rule "Ingredients Scoring"

	salience 2
	when
		$i: Ingredient($ingredientName: name, $benefitingAge: benefitingAge, $benefitingAcne: benefitingAcne, $benefitingSkintype: benefitingSkintype, $benefitingGoals: benefitingGoals)
		$r: RecommendationInput(diagnosedSkinType != null, $skinType: diagnosedSkinType, $ageGroup: ageGroup, $acneType: acneType, $wantedGoals: wantedGoals)
		Number ($ageScore: intValue) from accumulate(
			AgeGroup($age:this, this == $ageGroup) from $benefitingAge,
			count(1)
		)
		Number ($acneScore: intValue) from accumulate(
			AcneType($acne:this, this == $acneType) from $benefitingAcne,
			count(1)
		)
		Number ($skinTypeScore: intValue) from accumulate(
			SkinType($s:this, this == $skinType) from $benefitingSkintype,
			count(1)
		)
		Number ($goalScore: intValue) from accumulate(
        	$g: Goal( name memberOf $wantedGoals ) from $benefitingGoals,
        	count($g)
        )
	then 
		double score = 0.5 * $goalScore + $ageScore + $skinTypeScore + $acneScore;
		System.out.println("Ingredient: " + $ingredientName);
      	System.out.println("Score: " + score);
	  	insert( new IngredientScoring($ingredientName, score)); 
	end 
	
rule "Remove products with alergen"

	salience 1
	when 
		$p: Product($productName: name, $manufacturer: manufacturer, $ingredients: ingredients)
		$r: RecommendationInput($alergies: alergies)

        exists( Ingredient(name memberOf $alergies) from $ingredients )         	
        
	then 
		System.out.println("Remove product containing alergen: " + $productName);
		delete($p)
end

rule "Product Scoring"
	salience 0
	no-loop
	when
		$p: Product($productName: name, $ingredients: ingredients, $productType: productType)
		$r: RecommendationInput($manufacturer: manufacturer)
		$ingredientNames: List() from accumulate (
	        Ingredient($name: name) from $ingredients,
	        collectList($name)
	    )
		Number($ingredientS: intValue) from accumulate(
			$is: IngredientScoring($ingredientName: ingredient, $ingredientScore: score) and
        	IngredientScoring(ingredient memberOf $ingredientNames, $s: score) from $is,
        	sum($s)
        )
        Number ($manufacturerScore: intValue) from accumulate(
        	Product(manufacturer == $manufacturer) from $p,
			count(1)
		)
	then
	    double score = $ingredientS + $manufacturerScore;
		System.out.println("Product " + $productName + " Score: " + score);
		insert (new ProductScoring($productName, score, $productType));
end 

rule "Select best CLEANSER"
	salience (-100/$score)
	no-loop 
	when
		$scoredProducts: ProductScoring(
	    	productType == ProductType.CLEANSER,
	        $score: score,
	        $name: name
	    );
        not Serum();
	then
		System.out.println("CLEANSER " + $name + " " + $score);
    	insert( new Cleanser() );
    
    
end

rule "Select best EXFOLIATOR"
	salience (-100/$score)
	no-loop 
	when
		$scoredProducts: ProductScoring(
	    	productType == ProductType.EXFOLIATOR,
	        $score: score,
	        $name: name
	    );
        not Exfoliator();
	then
		System.out.println("EXFOLIATOR " + $name + " " + $score);
    	insert( new Exfoliator() );
    
end

rule "Select best SERUM"
	salience (-100/$score)
	no-loop 
	when
		$scoredProducts: ProductScoring(
	    	productType == ProductType.SERUM,
	        $score: score,
	        $name: name
	    );
        not Serum();
	then
		System.out.println("SERUM " + $name + " " + $score);
    	insert( new Serum() );
end


rule "Select best SUN_SCREEN"
	salience (-100/$score)
	no-loop 
	when
		$scoredProducts: ProductScoring(
	    	productType == ProductType.SUN_SCREEN,
	        $score: score,
	        $name: name
	    );
        not SunScreen();
	then
		System.out.println("SUN_SCREEN " + $name + " " + $score);
    	insert( new SunScreen() );
end


rule "Select best MOISTURIZER"
	salience (-100/$score)
	no-loop 
	when
		$scoredProducts: ProductScoring(
	    	productType == ProductType.MOISTURIZER,
	        $score: score,
	        $name: name
	    );
        not Moisturizer();
	then
		System.out.println("MOISTURIZER " + $name + " " + $score);
    	insert( new Moisturizer() );
    
end